# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    WeightedValidation(weighting, folding; lambda=1.0, loss=Dict())

An error estimation method which samples are weighted with
`weighting` method and split into folds with `folding` method.
Weights are raised to `lambda` power in `[0,1]`. Optionally,
specify `loss` function from `LossFunctions.jl` for some of
the variables.

## References

* Sugiyama et al. 2006. [Importance-weighted cross-validation for
  covariate shift](https://link.springer.com/chapter/10.1007/11861898_36)
* Sugiyama et al. 2007. [Covariate shift adaptation by importance weighted
  cross validation](http://www.jmlr.org/papers/volume8/sugiyama07a/sugiyama07a.pdf)
"""
struct WeightedValidation{W<:WeightingMethod,F<:FoldingMethod,T<:Real} <: ErrorMethod
  weighting::W
  folding::F
  lambda::T
  loss::Dict{Symbol,SupervisedLoss}

  function WeightedValidation{W,F,T}(weighting, folding, lambda, loss) where {W,F,T}
    @assert 0 â‰¤ lambda â‰¤ 1 "lambda must lie in [0,1]"
    new(weighting, folding, lambda, loss)
  end
end

WeightedValidation(weighting::W, folding::F; lambda::T=1.0, loss=Dict()) where {W,F,T} =
  WeightedValidation{W,F,T}(weighting, folding, lambda, loss)

function cverror(setup::ErrorSetup, geotable::AbstractGeoTable, method::WeightedValidation)
  ovars = _outputs(setup, geotable)
  loss = method.loss
  for var in ovars
    if var âˆ‰ keys(loss)
      v = getproperty(geotable, var)
      loss[var] = defaultloss(v[1])
    end
  end

  # weight all samples
  ws = weight(geotable, method.weighting) .^ method.lambda

  # folds for cross-validation
  fs = folds(geotable, method.folding)

  # error for a fold
  function Îµ(f)
    # fold prediction
    pred = _prediction(setup, geotable, f)

    # holdout set
    holdout = view(geotable, f[2])

    # holdout weights
    ð“Œ = view(ws, f[2])

    # loss for each variable
    losses = map(ovars) do var
      â„’ = loss[var]
      yÌ‚ = getproperty(pred, var)
      y = getproperty(holdout, var)
      var => mean(â„’, yÌ‚, y, ð“Œ, normalize=false)
    end

    Dict(losses)
  end

  # compute error for each fold in parallel
  Îµs = foldxt(vcat, Map(Îµ), fs)

  # combine error from different folds
  Dict(var => mean(get.(Îµs, var, 0)) for var in ovars)
end

# output variables
_outputs(::InterpSetup, gtb) = setdiff(propertynames(gtb), [:geometry])
_outputs(s::LearnSetup, gtb) = s.output

# prediction for a given fold
function _prediction(s::InterpSetup{I}, geotable, f) where {I}
  sdat = view(geotable, f[1])
  sdom = view(domain(geotable), f[2])
  sdat |> I(sdom, s.model; s.kwargs...)
end

function _prediction(s::LearnSetup, geotable, f)
  source = view(geotable, f[1])
  target = view(geotable, f[2])
  target |> Learn(source, s.model)
end
